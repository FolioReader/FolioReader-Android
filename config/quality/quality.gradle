/**
 * Set up Checkstyle, Findbugs and PMD to perform extensive code analysis.
 *
 * Gradle tasks added:
 * - checkstyle
 * - spotbugs
 * - pmd
 *
 * The three tasks above are added as dependencies of the check task so running check will
 * run all of them.
 */

apply plugin: 'checkstyle'
apply plugin: 'com.github.spotbugs'
apply plugin: 'pmd'

dependencies {
    checkstyle 'com.puppycrawl.tools:checkstyle:6.5'
}

def qualityConfigDir = "$project.rootDir/config/quality"
def reportsDir = "$project.buildDir/reports"

check.dependsOn 'checkstyle', 'spotbugs', 'pmd'

task checkstyle(type: Checkstyle, group: 'Verification', description: 'Runs code style checks') {
    configFile file("$qualityConfigDir/checkstyle/checkstyle-config.xml")
    source 'src'
    include '**/*.java'

    reports {
        xml.enabled = true
        xml {
            destination = file("$reportsDir/checkstyle/checkstyle.xml")
        }
    }

    classpath = files()
}

spotbugs {
    ignoreFailures = false
    effort = "max"
    reportLevel = "high"

    includeFilter = file("**/*.java")
    excludeFilter = file("**/gen/**")
}

tasks.withType(SpotBugsTask) {
    dependsOn 'assemble'
    group = "verification"
    excludeFilter = new File("$qualityConfigDir/spotbugs/android-exclude-filter.xml")
    classes = files("$project.rootDir/folioreader/build/intermediates/javac")

    source = fileTree('src')

    reports {
        xml.enabled = false
        html.enabled = true
        xml {
            destination = file("$reportsDir/spotbugs/spotbugs.xml")
        }
        html {
            destination = file("$reportsDir/spotbugs/spotbugs.html")
        }
    }

    classpath = files()
}

task pmd(type: Pmd, group: 'Verification', description: 'Inspect sourcecode for bugs') {
    ruleSetFiles = files("$qualityConfigDir/pmd/pmd-ruleset.xml")
    ignoreFailures = false
    ruleSets = []

    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    reports {
        xml.enabled = true
        html.enabled = true
        xml {
            destination = file("$reportsDir/pmd/pmd.xml")
        }
        html {
            destination = file("$reportsDir/pmd/pmd.html")
        }
    }
}